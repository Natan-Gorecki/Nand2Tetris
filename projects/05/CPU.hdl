// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=isAInstruction);
    And(a=instruction[15], b=true, out=isCInstruction);

    Mux16(a=instruction, b=aluOutput, sel=instruction[15], out=aRegisterInput);
    
    And(a=instruction[5], b=instruction[15], out=loadAfromALU);
    Or(a=loadAfromALU, b=isAInstruction, out=loadARegister);

    ARegister(in=aRegisterInput, load=loadARegister, out=aRegisterValue, out[0..14]=addressM);
    
    And(a=instruction[4], b=isCInstruction, out=loadDRegister);
    DRegister(in=aluOutput, load=loadDRegister, out=dRegisterValue);

    Mux16(a=aRegisterValue, b=inM, sel=instruction[12], out=aOrMRegisterValue);
    ALU(x=dRegisterValue, y=aOrMRegisterValue, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOutput, out=outM, zr=zr, ng=ng);
    
    And(a=isCInstruction, b=instruction[3], out=writeM);
    
    And(a=zr, b=true, out=isEqual);
    Not(in=isEqual, out=notZero);
    And(a=notZero, b=ng, out=isLess);
    Not(in=ng, out=notNegative);
    And(a=notZero, b=notNegative, out=isGreater);
    
    And(a=instruction[0], b=isGreater, out=jgt);
    And(a=instruction[1], b=isEqual, out=jeq);
    And(a=instruction[2], b=isLess, out=jlt);

    Or(a=jgt, b=jeq, out=jumpPart1);
    Or(a=jumpPart1, b=jlt, out=jumpPart2);
    And(a=isCInstruction, b=jumpPart2, out=jump);
    Not(in=jump, out=noJump);

    PC(in=aRegisterValue, load=jump, inc=noJump, reset=reset, out[0..14]=pc);
}